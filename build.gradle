apply plugin: 'java'
apply plugin: 'eclipse'

sourceCompatibility = 1.6
targetCompatibility = 1.6

archivesBaseName = "MCPatcher-Repackager"
version = "1.4"

repositories{
	mavenCentral()
	maven{
		name = "minecraft"
		url = "https://libraries.minecraft.net/"
	}
}
configurations{ redist }
sourceSets{
	runtime{ compileClasspath += main.compileClasspath }
	debug{}
}
dependencies{
	// only package with main
	redist 'com.google.code.gson:gson:2.2.4'
	redist 'commons-io:commons-io:2.4'
	compile configurations.redist
	// don't package
	compile ('net.minecraft:launchwrapper:1.11'){
		exclude group: 'org.lwjgl.lwjgl', module: 'lwjgl'
		exclude group: 'org.apache.logging.log4j'
		exclude group: 'org.ow2.asm', module:'asm-debug-all'
		exclude group: 'net.sf.jopt-simple', module: 'jopt-simple'
	}
}
task runtimeJar(type:Jar){
	from sourceSets.runtime.output
	archiveName "runtime.jar"
}
task copyJar(type:Copy){
	dependsOn runtimeJar
	from runtimeJar.archivePath
	into "src/debug/resources/"
}
task cleanJar(type:Delete){
	delete 'src/debug'
}
clean.dependsOn cleanJar
eclipseClasspath.dependsOn copyJar
jar{
	dependsOn copyJar
	from{ configurations.redist.collect{ it.isDirectory() ? it : zipTree(it) }}
	from file(runtimeJar.archivePath)
	exclude "**/maven/**"
	manifest{ attributes "Main-Class": "mnm.mcpackager.Repackage" }
}